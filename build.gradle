plugins {
    id('com.github.johnrengelman.shadow') version '8.1.1'
    id 'java-library'
    id 'maven-publish'
}

group = 'com.ferra13671'
version = '1.1'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
    withSourcesJar()
}

repositories {
    maven {
        url 'https://jitpack.io/'
    }
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation 'com.github.Querz:NBT:6.1'
    implementation 'com.google.code.gson:gson:2.11.0'
}

test {
    useJUnitPlatform()
}

shadowJar {
    dependencies {
        include(dependency('com.github.Querz:NBT:6.1'))
        include(dependency('com.google.code.gson:gson:2.11.0'))
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.ferra13671.SchematicGenerator.SchematicGenerator'
        )
    }
}

build {
    dependsOn(shadowJar)
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.ferra13671'
            artifactId = '2d-schematic-generator'
            artifact source: shadowJar, classifier: null
            artifact sourcesJar
        }
    }

    repositories {
        mavenLocal()
        maven {
            name = "GithubPackages"
            url = new URI("https://maven.pkg.github.com/Ferra13671/2d-schematic-generator")
            credentials.username = System.getenv("GITHUB_ACTOR")
            credentials.password = System.getenv("GITHUB_TOKEN")
        }
    }
}